1000	PRINT "DEPTH CHARGE GAME" 
1010	PRINT
1020	PRINT "DIMENSION OF SEARCH AREA"; 
1030	INPUT G
1040	PRINT
1050	N=INT(LOG(G)/LOG(2))+1 
1060	RANDOMIZE
1070	PRINT "YOU ARE CAPTAIN OF THE DESTROYER USS DIGITAL AND AN ENEMY SUB HAS"
1080	PRINT "BEEN CAUSING  YOU  TROUBLE,  YOUR MISSION  IS TO DESTROY IT.  YOU"
1090	PRINT "HAVE "N" SHOTS.  SPECIFY DEPTH CHARGE EXPLOSION POINT WITH A TRIO "
1100	PRINT "OF NUMBERS (DISTANCE NORTH, DISTANCE EAST, DEPTH FROM SURFACE)."
1110	PRINT 
1120	PRINT "GOOD LUCK !" 
1130	PRINT
1140	A=INT(G*RND) 
1150	B=INT(G*RND) 
1160	C=INT(G*RND)
1170	FOR D=1 TO N 
1180	PRINT 
1190	PRINT "TRIAL NUMBER ";D 
1200	PRINT "ENTER COORDINATES (N,E,D) : ";
1210	INPUT Y,X,Z
1220	IF ABS(X-A)+ABS(Y-B)+ABS(Z-C)=0 THEN 1300
1230	GOSUB 1390
1240	PRINT 
1250	NEXT D
1260	PRINT 
1270	PRINT "YOU HAVE BEEN TORPEDOED!  ABANDON SHIP!"
1280	PRINT "THE SUBMARINE WAS AT ";B,",";A,",";C
1290	GOTO 1340
1300	PRINT 
1310	PRINT "B O O M ! !  YOU FOUND IT IN ";D;"TRIES!"
1320	PRINT 
1330	PRINT 
1340	PRINT "ANOTHER GAME (Y OR N)";
1350	INPUT A$
1360	IF A$="Y" THEN 1010
1370	PRINT "OK.  HOPE YOU ENJOYED YOURSELF " 
1380	GOTO 1650
1390	PRINT "SONAR REPORTS SHOT WAS ";
1400	IF Y>B THEN 1470
1410	IF Y<B THEN 1490
1420	IF X>A THEN 1510
1430	IF X<A THEN 1530
1440	IF Y<>B THEN 1550
1450	IF X<>A THEN 1550
1460	GOTO 1560
1470	PRINT "NORTH";
1480	GOTO 1420
1490	PRINT "SOUTH";
1500	GOTO 1420
1510	PRINT "EAST";
1520	GOTO 1440
1530	PRINT "WEST";
1540	GOTO 1440
1550	PRINT " AND ";
1560	IF Z>C THEN 1590
1570	IF Z<C THEN 1610
1580	IF Z=C THEN 1630
1590	PRINT "TOO LOW"
1600	GOTO 1640
1610	PRINT "TOO HIGH"
1620	GOTO 1640
1630	PRINT "DEPTH OK"
1640	RETURN
1650	END
